#!/usr/bin/env groovy

// Nightly jobs are configured for critical release branches in Jenkins.
//
// To set up a new nightly job, configure a new job with the following by hand
// or by machine:
//
// properties([
//   disableConcurrentBuilds(),
//   // Schedule a daily build between 6:00am and 6:30am UTC (10:00pm-10:30pm PST)
//   pipelineTriggers([cron('H(0-30) 06 * * 1-5')]),
//   parameters([
//     choice(choices: ["run", "skip"].join("\n"),
//            description: 'Run or skip robotest system wide tests.',
//            name: 'RUN_ROBOTEST'),
//     choice(choices: ["true", "false"].join("\n"),
//            description: 'Destroy all VMs on success.',
//            name: 'DESTROY_ON_SUCCESS'),
//     choice(choices: ["true", "false"].join("\n"),
//            description: 'Destroy all VMs on failure.',
//            name: 'DESTROY_ON_FAILURE'),
//     choice(choices: ["true", "false"].join("\n"),
//            description: 'Abort all tests upon first failure.',
//            name: 'FAIL_FAST'),
//   ]),
// ])
//
// The above Jenkins job config code is intentionally not active in this file
// because of issue #1315 -- 2020-04 walt

def withBuildResult(Closure body) {
  def previousBuildResult = currentBuild.previousBuild?.result
  try {
    body()
    currentBuild.result = 'SUCCESS'
  } catch(InterruptedException e) {
    currentBuild.result = 'ABORTED'
    throw e
  } catch(e) {
    currentBuild.result = 'FAILURE'
    throw e
  } finally {
    sendBuildNotification(currentBuild.result, previousBuildResult)
  }
}

/*
* sendBuildNotification sends a notification to slack if the build
* has recovered from a failure (i.e. had failed previously)
* or failed.
* See https://github.com/jenkinsci/jenkins/blob/jenkins-2.121.1/core/src/main/java/hudson/model/Result.java
* for the list of possible values for currentBuild.result.
*/
def sendBuildNotification(String currentBuildResult, String previousBuildResult) {
  currentBuildResult = currentBuildResult ?: "SUCCESS"
  previousBuildResult = previousBuildResult ?: "SUCCESS"

  def color = "warning"
  def message = "${env.JOB_NAME} ${env.BUILD_NUMBER} (<${env.BUILD_URL}|Open>)"
  if (currentBuildResult == "SUCCESS") {
    color = "good"
  }
  def shouldNotify = currentBuildResult != "STARTED" &&
                     currentBuildResult != "SUCCESS" &&
                     currentBuildResult != "ABORTED" ||
                    (currentBuildResult == "SUCCESS" && previousBuildResult != "SUCCESS")
  if (shouldNotify) {
    slackSend(color: color, message: message)
  }
}

timestamps {
  withBuildResult {
    node {
      stage('checkout') {
        checkout scm
        sh "git submodule update --init --recursive"
        sh "sudo git clean -ffdx" // supply -f flag twice to force-remove untracked dirs with .git subdirs (e.g. submodules)
      }
      stage('params') {
        echo "Jenkins Job Parameters:"
        for (param in params) { echo "${param}" }
      }
      stage('clean') {
        sh "make -C e clean"
      }
      stage('build-gravity') {
        withCredentials([
        [$class: 'SSHUserPrivateKeyBinding', credentialsId: '08267d86-0b3a-4101-841e-0036bf780b11', keyFileVariable: 'GITHUB_SSH_KEY'],
        [
          $class: 'UsernamePasswordMultiBinding',
          credentialsId: 'jenkins-aws-s3',
          usernameVariable: 'AWS_ACCESS_KEY_ID',
          passwordVariable: 'AWS_SECRET_ACCESS_KEY',
        ],
        ]) {
          sh 'make -C e production telekube-intermediate-upgrade opscenter'
        }
      }
    }
    throttle(['robotest']) {
      node {
        stage('build-and-test') {
          parallel (
          build : {
            withCredentials([
            [$class: 'SSHUserPrivateKeyBinding', credentialsId: '08267d86-0b3a-4101-841e-0036bf780b11', keyFileVariable: 'GITHUB_SSH_KEY']]) {
              sh 'make test && make -C e test'
            }
          },
          robotest : {
            if (env.RUN_ROBOTEST == 'run') {
              withCredentials([
                  [$class: 'FileBinding', credentialsId:'ROBOTEST_LOG_GOOGLE_APPLICATION_CREDENTIALS', variable: 'GOOGLE_APPLICATION_CREDENTIALS'],
                  [$class: 'FileBinding', credentialsId:'OPS_SSH_KEY', variable: 'SSH_KEY'],
                  [$class: 'FileBinding', credentialsId:'OPS_SSH_PUB', variable: 'SSH_PUB'],
                  ]) {
                    sh 'make -C e robotest-run-nightly'
              }
            } else {
              echo 'skipped system tests'
            }
          } )
        }
      }
    }
  }
}
